module Uint16Test

include "uint16"
from Uint16 use {
  fromNumber,
  toNumber,
  fromInt16,
  incr,
  decr,
  lnot,
  (+),
  (-),
  (*),
  (/),
  (%),
  (&),
  (|),
  (^),
  (<<),
  (>>),
}

// Suppress warnings about using `fromNumber` on constants, since that's what we want to test.
let fromNumber = fromNumber

// For testing on number with garbage leading bits
let oneWithGarbage = (1uS << 16uS) + 1uS
assert oneWithGarbage == 1uS

assert fromNumber(5) == 5uS
assert fromNumber(0) == 0uS
assert fromNumber(0xffff) == 0xffffuS
assert fromNumber(1) == oneWithGarbage

assert toNumber(55uS) == 55
assert toNumber(0uS) == 0
assert toNumber(0xffffuS) == 0xffff
assert toNumber(oneWithGarbage) == 1

assert fromInt16(1S) == 1uS
assert fromInt16(-1S) == 0xffffuS

assert 1uS + 0uS == 1uS
assert oneWithGarbage + oneWithGarbage == 2uS
assert 0xffffuS + 1uS == 0uS

assert 2uS - oneWithGarbage == 1uS
assert 100uS - 5uS == 95uS
assert 1uS - 2uS == 0xffffuS

assert 1uS * 0uS == 0uS
assert 5uS * oneWithGarbage == 5uS
assert 10uS * 6uS == 60uS
assert 256uS * 256uS == 0uS

assert 4uS / 2uS == 2uS
assert 4uS / oneWithGarbage == 4uS
assert 10uS / 100uS == 0uS
assert oneWithGarbage / 2uS == 0uS

assert 4uS % 1uS == 0uS
assert 4uS % oneWithGarbage == 0uS
assert 10uS % 3uS == 1uS
assert 0xffffuS % 10uS == 5uS
assert oneWithGarbage % 5uS == 1uS

assert lnot(0uS) == 0xffffuS
assert lnot(0xf0f0uS) == 0x0f0fuS

assert (0b1010uS & 0b10uS) == 0b10uS
assert (0b1010uS & 0uS) == 0uS

assert (0b1010uS | 0b0101uS) == 0b1111uS
assert (0b1010uS | 0uS) == 0b1010uS

assert (0b1010uS ^ 0b1101uS) == 0b0111uS
assert (0b1010uS ^ 0uS) == 0b1010uS

assert (1uS << 1uS) == 2uS
assert (3uS << oneWithGarbage) == 6uS
assert (oneWithGarbage << 2uS) == 4uS
assert (1uS << 16uS) == 0uS
assert (24uS << oneWithGarbage) == 48uS

assert (4uS >> oneWithGarbage) == 2uS
assert (oneWithGarbage >> 1uS) == 0uS
assert (4uS >> 2uS) == 1uS
assert (4uS >> 3uS) == 0uS

assert incr(0uS) == oneWithGarbage
assert incr(0xffffuS) == 0uS
assert decr(1uS) == 0uS
assert decr(0uS) == 0xffffuS

match (oneWithGarbage) {
  0uS => fail "oneWithGarbage uint16 matched to 0 incorrectly",
  0x1uS => void,
  _ => fail "oneWithGarbage uint16 incorrectly matched",
}

let arr = [> 1, 2, 3]
assert arr[toNumber(oneWithGarbage)] == 2

assert compare(0uS, 1uS) < 0
assert compare(2uS, oneWithGarbage) > 0
assert compare(0uS, 0uS) == 0
assert compare(0xffffuS, 0uS) > 0
assert compare(1uS, 0xffffuS) < 0

assert toString(123uS) == "123"
assert toString(oneWithGarbage) == "1"
assert toString(0xffffuS) == "65535"

from Uint16 use { (==), (!=), (<), (<=), (>), (>=) }

assert 1uS == 1uS
assert oneWithGarbage == 1uS
assert !(1uS == 2uS)

assert 1uS != 2uS
assert !(1uS != 1uS)

assert 5uS > oneWithGarbage
assert 5uS >= 5uS
assert 5uS < 0xffffuS
assert 5uS <= 5uS
assert !(5uS > 5uS)
assert !(0xffffuS <= 4uS)
assert !(5uS < 0uS)
assert !(5uS <= 4uS)
